import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}
group = 'nl.knaw.huc.di'
version = '1.0-SNAPSHOT'

apply plugin: 'application'

mainClassName = 'nl.knaw.huc.di.sesame.SesameApplication'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    jcenter()
    maven {
        url "http://maven.huygens.knaw.nl/repository"
    }
}

def getBuiltBy = { ->
    def user = System.getProperty("user.name")
    def host = InetAddress.localHost.hostName
    def os = System.getProperty("os.name") + ' ' + System.getProperty("os.version")
    return user + '@' + host + ' on ' + os
}

ext {
    dropwizardVersion = '1.3.5'
    junitVersion = '5.1+'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    implementation group: 'io.dropwizard', name: 'dropwizard-auth', version: dropwizardVersion
    implementation group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    implementation group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion

// despite excluding all logging dependencies, this 'implementation dependency' makes
// the app hang with a java.lang.IllegalStateException: Unable to acquire the logger context
//    implementation('nl.knaw.huygens:security-client:2.3.5') {
//        exclude group: 'org.slf4j', module: 'slf4j-api'
//        exclude group: 'org.slf4j', module: 'log4j12'
//        exclude group: 'org.slf4j', module: 'jul-to-slf4j'
//        exclude group: 'log4j', module: 'log4j'
//    }

    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion
    testRuntime group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC")).format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': getBuiltBy()
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
    archiveName 'open-sesame-full.jar'
}

test {
    useJUnitPlatform()
    failFast = true
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}
